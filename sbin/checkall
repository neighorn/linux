#! /usr/bin/perl

use strict;
no strict 'refs';
use warnings;
use Sys::Syslog;
use Getopt::Std;
use IO::Socket;
use File::Temp qw(tempfile);
use POSIX qw(strftime);
use Fcntl;
use FindBin qw($RealBin $RealScript);
require CheckAll::CheckItem;

my $HIGHLIGHT = "\e[33;40m";
my $RESET = "\e[0m";

our $opt_c='';			# Configuration file (service list).
our $opt_h=0;			# Help
our $opt_v=0;			# Verbose option.
our $opt_t=0;			# Test option.
our $opt_q=0;			# Quiet option.
our $opt_P='';			# Who to page on errors.
our $opt_R=120;			# Set number of minutes to re-report down items.
our $opt_s='';			# Alert subject
our $Prog = $RealScript;	# Get our program name.
$Prog=~s/\.pl$|\.bat$//;       	# Trim off the suffix, if present.
$Prog=~s".*[/\\]"";		# Trim off path, if present.
my @ItemList;			# List of item objects to check.
my @NowDown;			# Newly down items.
my @NowUp;			# Newly up items.
my @StillDown;			# On-going down items.
my %DownItems;			# List of items previously down.
my @OtherItems;			# Stuff in the status file from other check lists.
$| = 1;				# Unbuffer output.
my $StartTime = time();

my $StatusFile="$ENV{HOME}/.$Prog.dat";
my $ServiceList="/usr/local/etc/$Prog.list";

my $HostName = `hostname`;
chomp $HostName;

getopts('c:hP:qR:s:tv');

# Check options.
if ($opt_h) {
	system("pod2text $RealBin/$RealScript");
	exit 1;
}
if ($opt_s) {
	$opt_s =~ s/%C/$HostName/g;
}
else {
	$opt_s = "$HostName alert";
}

openlog("$Prog");


my $DownMessages;
if ($opt_q) {
	# Prepare the Down-Message header text.
	my($Minutes,$Hours)=(localtime())[1,2];
	$Minutes="0$Minutes" if ($Minutes <= 9);
	$DownMessages="The following services were detected down at 
$Hours:$Minutes...";
}

my $errors=0;
my %PIDs;		# Map PID to @ItemList.
my %NameMap;		# Maps status names to @ItemList.

# Prepare the list of files to process.
foreach (@ARGV) {
	# Add a path and file name prefix to parameters (not -c).
	s"^"/usr/local/etc/$Prog.";
}
push @ARGV,$opt_c if ($opt_c);	# Use opt_c for files in other locations.
push @ARGV,$ServiceList unless (@ARGV+0 > 0);	# Use default if nothing else.
foreach (@ARGV) {
	LoadServiceList($_,\@ItemList,\%NameMap);	# Load this file.
}

if (@ItemList+0 == 0) {
	# Nothing to monitor.
	warn "No services found in $ServiceList to monitor.";
	push @NowDown,"No services found to monitor";
}

# Load any prior statuses.
LoadPriorStatuses($StatusFile, \%NameMap, \@OtherItems);

# Go check all these services.
my $Services = @ItemList + 0;
my $DescLen = 0;		# Will keep length of longest desc here.
foreach my $Item (@ItemList) {
	my $CheckResult = Check $Item;
	my($Type,$Value) = ($CheckResult =~ m/^(\S+)=(\d+)$/);
	
	if ($Type eq 'Status') {		# 5.8 doesn't have switch.
		# This one returned a status immediately.
		$Item->Status($Value);
		$Services--;			# Not waiting on this one.
	}
	elsif ($Type eq 'PIDList') {
		# This one is running in the background.  Map the PID to the item.
		$PIDs{$Value} = $Item;
	}
	else	{
		# Somebody sent back a response we don't understand.
		die qq[Unexpected response "$CheckResult" from $Item->{'Desc'}];
	}

	# Track the length of our longest description for formatting purposes, but ignore headings.
	if (ref $Item ne 'heading') {
		my $Len = length($Item->{Desc});
		$DescLen = $Len if ($DescLen < $Len);
	}
}

# All background processes submitted.  Wait for the results to come in.
my $pid;
`tty -s`;	# Find out if we have a tty.
my $no_tty = $?;
if ($Services) {
	do {
		print "Waiting for results on $Services service" . ($Services==1?'':'s') . "   \r"
			unless ($no_tty or  $opt_q);
		$pid = waitpid(-1,0);
		$PIDs{$pid}->Status($?>>8) if ($pid > 0);
		$Services--;
	} while $pid > 0;
	# Wipe out waiting message.
	print ' 'x40 . "\r" unless ($no_tty or  $opt_q);
}

# All results are in.  Run back through our report and list the results.
my $Errors = 0;
my $Renotify = 0;		# Set to 1 if no changes but someone due for renotification.
$DescLen += 2;				# Add two blanks for padding.
foreach my $Item (@ItemList) {
	# Call report to issue visible status, and count failures.
	my $Desc = $Item->Desc;
	my $Status = $Item->Report($DescLen);
	if ($Status == 0) {
		#CHECK_STILL_UP -- can't get constants to export.
		# syslog('INFO','%s is still up',$Desc)  - don't fill log with "still up".
	 	#	if (ref $Item ne 'heading' and $^O !~ /MSWin/);
	}	
	elsif ($Status == 1) {
		#CHECK_STILL_DOWN {
		syslog('INFO','%s is still down',$Desc)
			if ($^O !~ /MSWin/);
		push @StillDown, $Desc;
		$Renotify++
			 if ((time() - $Item->PriorNotification) >
				60 * $Item->Renotifyinterval);
		$Errors++;
	}
	elsif ($Status == 2) {
		#CHECK_NOW_UP {
		syslog('INFO','%s is NOW up',$Desc)
			if ($^O !~ /MSWin/);
		push @NowUp, $Desc;
		if (my $Action = $Item->Onup) {
			if ($opt_t) {
				print "\t\tTesting: OnUp for $Desc suppressed.\n";
			}
			else {
				print "Triggering OnUp for $Desc\n" if ($opt_v);
				$Action =~ s/%D/$Desc/g;
				system("($Action)&");
				syslog('INFO',"Executed OnUp actions for $Desc.  Status $?")
					if ($^O !~ /MSWin/);
			}
		}
	}
	elsif ($Status == 3) {
		#CHECK_NOW_DOWN {
		syslog('INFO','%s is NOW down',$Desc)
			if ($^O !~ /MSWin/);
		push @NowDown, $Desc;
		if (my $Action = $Item->Ondown) {
			if ($opt_t) {
				print "\t\tTesting: OnDown action for $Desc suppressed.\n";
			}
			else {
				print "Triggering OnDown for $Desc\n" if ($opt_v);
				$Action =~ s/%D/$Desc/g;
				system("($Action)&");
				syslog('INFO',"Executed OnDown actions for $Desc.  Status $?")
					if ($^O !~ /MSWin/);
			}
		}
		$Item->FirstDown($StartTime);	# Remember when it went down.
		$Errors++;
	}
	else {
		die "$Item->{FILE}:$Item->{LINE} Unexpected status $Status returned.";
	}
}

if (@NowDown+@NowUp+$Renotify > 0 and $opt_P and ! $opt_t) {
	my($MAIL,$MailFile) = tempfile();
	
	if (@NowDown) {
		print $MAIL "Now down: " . join(', ',@NowDown) . "\n";
	}
	if (@StillDown) {
		print $MAIL "Still down: " . join(', ',@StillDown) . "\n";
	}
	if (@NowUp) {
		print $MAIL "Now up: " . join(', ',@NowUp) . "\n";
	}
	printf $MAIL strftime("\nFrom $HostName at %T on %D\n",localtime());
	close $MAIL;
	system("sed 's/  */ /g' $MailFile | mail -s \"$opt_s\" $opt_P") && warn "Unable to send mail: $!\n";
	unlink $MailFile;
	
	# Update all the notification times.
	foreach my $Item (@ItemList) {
		$Item->PriorNotification($StartTime);
	}
}

# Write the status file, so we know "still down" from "now down".
WriteStatusFile($StartTime,$StatusFile,\@ItemList,\@OtherItems) unless ($opt_t);

if (@NowDown+@StillDown > 0) {
	printf "%s==> %d " . ($Errors == 1?'service is':'services are') . " down.%s\n",
		$HIGHLIGHT, $Errors, $RESET
		unless ($opt_q);
	exit 8;
}
else {
	print "All services are up.\n" unless ($opt_q);
	syslog('INFO',"All services are up.")
		if ($^O !~ /MSWin/);
	exit 0;
}




#
# LoadServiceList - load a list of services.
#
sub LoadServiceList {
	my($ServiceList,$ArrayRef,$MapRef) = @_;
	return unless (-f $ServiceList);
	my $FH;
	if (sysopen($FH,$ServiceList,O_RDONLY)) {
		# Load the service file.
		my $LineInProgress = '';
		my $CompleteLine = '';
		my $StartLine;
		my $LineNum = 0;
		while (<$FH>) {
			$LineNum++;
			next if (/^\s*#/);			# Skip comments.
			next if (/^\s*$/);			# Skip blank lines.
			chomp;
			s/\s+$//;				# Strip trailing blanks.

			# Is this a continuation line (starts with white space)?
			if (/^\s+/) {
				# Yes.  Append it to any prior data.
				s/^\s+//;
				$LineInProgress .= " $_";	# Append to prior data.
			}
			else {
				# This is (the beginning of?) a new line.  Process the old line if we have one.
				ProcessServiceLine($ServiceList, $StartLine, $ArrayRef, $MapRef, $LineInProgress);
				$LineInProgress = $_;
				$StartLine = $LineNum;
			}
		}
		# Process the last line if we have one.
		ProcessServiceLine($ServiceList, $StartLine, $ArrayRef, $MapRef, $LineInProgress);	
		close $FH;
	}
}



#
# ProcessServiceLine : process one logical line from a service file.
#
sub ProcessServiceLine {
	my($FileName, $StartLine, $ArrayRef, $MapRef, $Line) = @_;
	return if ($Line =~ /^\s*$/);		# Skip empty lines.

	my($Keyword,$Rest) = split('\s+',$Line,2);
	$Keyword = lc($Keyword);				# Ignore case.
	if ($Keyword eq 'include') {
		# Include file.
		$Rest =~ s/^(["'])(.*)\1$/$2/;	# Strip quotes.
		LoadServiceList($Rest,$ArrayRef,$MapRef);
		return 0;
	}

	# Must be a check type.  Load it if it isn't already loaded.
	if (!exists($INC{"CheckAll/$Keyword.pm"})) {
	#		eval("use CheckAll::$Keyword;");
		eval qq[require "CheckAll/$Keyword.pm"];
		if ($@) {
			warn qq[$ServiceList:$StartLine: Unable to include a "$Keyword" module -- skipped\n];
			return 1;
		}
	}

	# Create this object and put it in @ItemList.
	my $Item;
	eval {
		$Item = $Keyword->new (
			"$ServiceList",	# For object error msgs.
			"$StartLine",	# For object error msgs.
			$Rest);		# Other parameters.
	};
	if ($@) {
		warn qq[$ServiceList:$StartLine: Unable to create a "$Keyword" item: $@\n];
		return 2;	
	}
	else {
		# Set the unique name if it isn't already set.
		$Item->Name(ref($Item) . "=" . $Item->Target) unless ($Item->Name);

		# Map the name to this item.
		$MapRef->{$Item->Name} = $Item;
		push @$ArrayRef,$Item;
		return 0;
	}
}



sub LoadPriorStatuses {

	my($StatusFile,$MapRef,$OtherRef) = @_;

	# Load table of items that have already been reported down.
	my $STATUS;
	if (-f $StatusFile) {
		if (sysopen($STATUS,$StatusFile,O_RDONLY)) {
			# Load up any items that were previously down.
			while (<$STATUS>) {
				next unless
					(/^Name=(.*), DownAt=(\d+)=\S+ \S+, LastNotify=(\d+)=\S+ \S+, LastCheck=(\d+)=\S+ \S+,?/);
				my($Name,$FirstDown,$LastNotify,$LastCheck)=($1,$2,$3,$4);
				if(exists($MapRef->{$Name})) {
					$MapRef->{$Name}->{PriorStatus} = 8;
					$MapRef->{$Name}->{FirstDown} = $FirstDown;
					$MapRef->{$Name}->{PriorNotification} = $LastNotify;
				}
				elsif ((time()-$LastCheck)/86400 <= 60) {
					# Not one of ours.  Must come from another config file.  Just 
					# store it to write back later.
					push @$OtherRef,$_;
				}
				else {
					# Nobody has checked this one for 60 days.  Forget it.
				}
			}
			close $STATUS;
		}
		else {
			warn "Unable to open $StatusFile: $!\n";
		}
	}
}




sub WriteStatusFile {

	my($StartTime,$StatusFile,$ArrayRef,$OtherRef) = @_;
	my $STATUS;
	if (open ($STATUS,"> $StatusFile")) {
		foreach my $Item (@$ArrayRef) {
			next if ($Item->Status eq 0);		# Skip up services.
			next if (ref $Item eq 'history');	# We don't track history lines.
			printf $STATUS "Name=%s, DownAt=%d=%s, LastNotify=%d=%s, LastCheck=%d=%s, File=%s:%d\n",
				$Item->Name,
				$Item->FirstDown,
				strftime("%D %T",localtime($Item->FirstDown)),
				$Item->PriorNotification,
				strftime("%D %T",localtime($Item->PriorNotification)),
				$StartTime,
				strftime("%D %T",localtime($StartTime)),
				$Item->{FILE},
				$Item->{LINE}
			;
		}
		# Write out stuff we found in the status file that weren't ours.
		foreach (@$OtherRef) {
			print $STATUS $_;
		}
		close $STATUS;
	}
	else {
		warn("Unable to update $StatusFile: $!\n");
	}
}
=pod

=head1 checkall -- check on services

=head2 Overview

checkall checks critical services to make sure they're running.  It can optionally 
take action when a service comes up or goes down, and/or send out notifications.

=head2 Command format:

   checkall [options] [listname ...]

=head2 Command options:

=over 3

=item -c file

Service file -- normally the service to monitor are determined by reading files
from /usr/local/etc/checkall.list (default) or /usr/local/etc/checkall.I<listname>
if one or more list names are provided on the command line.  The -c option provides an
alternate way of identifying a service file, when the desired file isn't named according
to the "listname" standard (i.e. isn't located in /usr/local/etc).

=item -h

Help -- print this documentation

=item -P address

Page -- send a message to this e-mail address whenever services transition from up
to down or vice versa, or if enough time has elapsed to warrant renotification.

=item -q

Quiet - suppress status messages.

=item -R minutes

Renotify -- send out another page if a service has been down for this many minutes
since the last page.  Default is 120 (2 hours).

=item -s subject

Subject -- use this subject line when sending out pages.  "%C" is replaced with the
host ("Computer") name.

=item -v

Verbose -- turn on diagnostics.

=back

=head2 Service List Files

checkall determines what services to monitor by using service list files.  The default
service list is "/usr/local/etc/checkall.list".  Alternate or additional lists can be
saved as "/usr/local/etc/checkall.I<name>", where "name" matches the name specified on the
command line (e.g. /usr/local/etc/checkall.urgent would be referenced by "checkall urgent").
Service lists that don't follow this naming convention can be specified using the -c option.

Service list files are formatted according to the following rules:

=over 3

=item *

Blank lines and lines starting with # are ignored

=item *

Lines beginning with white space are treated as continuation lines.

=item *

A service list file may include another service list file using the "include" directive as follows:

   include path-to-file

=item *

All other lines designate services to check.  The general format of a service line is:

   checktype parameters

"checktype" refers to a Perl check module called "CheckAll::I<checktype>".  This module will be 
loaded if necessary, and passed the parameters provided.  Most modules require at least a
Target= parameter and a Desc= parameter.  See the documentation in CheckAll::CheckItem for common
parameters, and other modules in the CheckAll:: tree for kinds of checks and specific parameters.

The most common checks are "tcpport", which checks for connectivity to a TCP socket, and "process", which
checks the process table for a running process.  "heading" is also used to insert headings.
=back

=cut

