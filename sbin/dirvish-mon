#!/usr/bin/perl
#
# Dirvish-mon - monitor Dirvish jobs
#
use strict;
use warnings;
use Getopt::Long qw(GetOptionsFromArray :config gnu_compat permute bundling);
use Fcntl;
use POSIX qw(strftime);
use File::Glob ('bsd_glob');
use File::ReadBackwards;
use JobTools::Utils qw(:Opt CompressByteSize);

# Initialize variables.
my $Prog=$0;			# Get our name, for messages.
$Prog=~s/\.pl$|\.bat$//;	# Trim off the suffix, if present.
$Prog=~s".*[/\\]"";		# Trim off the path, if present.
$ENV{PATH} = '/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin';
my @Args=@ARGV;			# Preserve orig command for ref.
my $ErrorFlag=0;		# No errors so far.
my @ConfigFiles=("/usr/local/etc/${Prog}.cfg");     # Name of config files.
our $Errors=0;
our %Config;
our @Parms;
our %Options;			# Options settings.
JobTools::Utils::init(config => \%Config, options => \%Options);

# Define our command-line options.  I use sub for everything, because
# GetOptions doesn't work right on a second call to it (which we need
# to do) with the conventional approach.
$DB::AutoTrace=$DB::AutoTrace;		# Suppress spurious warning.
my %OptionSpecifications;
%OptionSpecifications=(
	'<>'			=>	sub {my $Arg = shift; push @Parms,$Arg if (length($Arg));},
#	'always-mail|m=s'	=>	\&OptArray,
#	'always-page|p=s'	=>	\&OptArray,
#	'debug|d'		=>	sub {$DB::AutoTrace=1;},
#	'error-mail|M=s'	=>	\&OptArray,
#	'error-page|P=s'	=>	\&OptArray,
#	'filter-file|F=s'	=>	\&OptArray,
	'count=n'		=>	\&OptValue,
	'help|h|?!'		=>	\&opt_h,
	'html!'			=>	\&OptFlag,
	'interval=n'		=>	\&OptValue,
	'option-set|O=s'	=>	sub {OptOptionSet(name => $_[1],optspec => \%OptionSpecifications);},
#	'remote|R=s'		=>	sub {OptArray(@_,'allow-delete'=>1,'expand-config'=>1);},
	'test|t'		=>	\&OptFlag,
	'verbose|v'		=>	\&OptFlag,
);
our $ExitCode;

# ---------------------------------------------------------
#
# Process the command line options.
#
%Options=(verbose => 0);        # Initialize Options.
die "Invalid options specified\n" unless (GetOptions(%OptionSpecifications));
$Options{interval} = 30 unless ($Options{interval} and $Options{interval} =~ /^[1-9]\d+/);

# ---------------------------------------------------------
#
# Verify the command line and run the job.
#
die('Excess parameters on the command line: "' . join(' ',@Parms) . "\" See \"$Prog -h\" for usage.")
	if (@Parms);


# ---------------------------------------------------------
#
# Gather and display the data in a loop.
#

while ( ! exists($Options{count}) or ($Options{count} > 0)) {
	my $StartTime = time();
	RunCycle();
	if (exists($Options{count})) {
		$Options{count}--;
		last if ($Options{count} < 1);
	}
	my $SleepTime = $Options{interval} - (time() - $StartTime);
	sleep($SleepTime);
}

exit(0);


sub RunCycle {

	my %LogFiles;
	my $LSOF;
	my $VaultMax = 0;	# Length of longest vault name.
	if (open($LSOF,'lsof -c rsync -b 2> /dev/null |')) {
		while (<$LSOF>) {
			chomp;
			print "Verbose: lsof returned: $_\n" if ($Options{verbose}>=3);
			# rsync   3218 root    1w   REG              252,0     22510 36964139 /usr/local/dirvish/vinnie/2017-05-24-Wed-test/log.tmp
			next unless (m"^rsync\s+(\d+)\s+\S+\s+\S+\s+REG\s+\S+\s+\d+\s+\d+\s+(\S*/log.tmp)\s*$");
			my ($pid,$logfile) = ($1,$2);
			print "Verbose: Found PID $pid and logfile $logfile\n" if ($Options{verbose}>=2);
			next if (exists($LogFiles{$logfile}));		# Have already seen this file.
			$LogFiles{$logfile}->{pid} = $pid;		# Save the PID
			my $vault = $logfile;
			$vault =~ s"/log.tmp$"";			# Extract the vault path.
			$LogFiles{$logfile}->{vaultpath} = $vault;	# Save the vault path.
			$vault =~ s"^.*/(\S+/\S+)$"$1";			# Extract just the vault name.
			$LogFiles{$logfile}->{vault} = $vault;		# Save the vault name.
			$VaultMax = length($vault) if (length($vault) > $VaultMax);
			my $space = `df -hlP "$LogFiles{$logfile}->{vaultpath}" | grep -v ^Filesystem`;
			$space =~ s/^\S+\s+\S+\s+\S+\s+\S+\s+(\S+)\s//;
			$LogFiles{$logfile}->{percentfull} = $1;
		}
		close $LSOF;
	}
	
	my $LatestMax = 0;	# Length of longest name.
	foreach my $logfile (keys(%LogFiles)) {
		my $LOGFH = File::ReadBackwards->new( $logfile ) or next;
		until ($LOGFH->eof) {
			my $line = $LOGFH->readline;
			next unless ($line =~ /^[c>h.]([fdLDS])\S*\s+(.*?)\s*$/);
			$LogFiles{$logfile}->{type} = $1;
			$LogFiles{$logfile}->{lastfile} = $2;
			$LatestMax = length($2) if (length($2) > $LatestMax);
			last;
		}
		if (! exists($LogFiles{$logfile}->{type})) {
			# Nothing in the log yet.
			$LogFiles{$logfile}->{type} = '-';
			$LogFiles{$logfile}->{lastfile} = '(none yet)';
		}
		$LOGFH->close;
		my $mtime = (stat($logfile))[9];
		if ($LogFiles{$logfile}->{lastfile}
			and ($LogFiles{$logfile}->{type} eq 'f')
		) {
			my $lastfile = $LogFiles{$logfile}->{vaultpath} . "/tree/" . $LogFiles{$logfile}->{lastfile};
			if ( ! -e $lastfile) {
				# Being written under a temporary file name.
				$lastfile =~ s"(.*/)([^/]*)$"$1.$2.*";
				$lastfile = bsd_glob($lastfile);
			}
			my $size;
			if (defined($lastfile)) {
				($size,$mtime) = (stat($lastfile))[7,9];
				$LogFiles{$logfile}->{size} = CompressByteSize($size);
				# Use either log mtime or file mtime, which ever is more recent.
				$LogFiles{$logfile}->{mtime} = $mtime
					if ( !defined($LogFiles{$logfile}->{mtime}) 
						or (defined($mtime) && $LogFiles{$logfile}->{mtime} < $mtime));
			}
			else {
				$LogFiles{$logfile}->{size} = '?';
			}
		}
		else {
			if ($LogFiles{$logfile}->{type} eq 'd') {
				$LogFiles{$logfile}->{size} = '(dir)';
			}
			elsif ($LogFiles{$logfile}->{type} eq 'L') {
				$LogFiles{$logfile}->{size} = '(symlink)';
			}
			elsif ($LogFiles{$logfile}->{type} eq 'D') {
				$LogFiles{$logfile}->{size} = '(device)';
			}
			elsif ($LogFiles{$logfile}->{type} eq 'S') {
				$LogFiles{$logfile}->{size} = '(socket)';
			}
			else {
				$LogFiles{$logfile}->{size} = '(' . $LogFiles{$logfile}->{type} . ')';
			}
			
		}
		$LogFiles{$logfile}->{mtime} = 
			POSIX::strftime('%T',localtime($mtime));	# Make human-readable.
	}
	
	DisplayData(\%LogFiles,$VaultMax,$LatestMax);
}


# ---------------------------------------------------------
#
# DisplayData - display what we found.
#
sub DisplayData {
	my($LogRef,$VaultMax,$LatestMax) = @_;
	if (keys(%$LogRef) == 0) {
		print "No rsync processes running.\n";
		return;
	}
	if ($Options{html}) {
		# Display as HTML
		#  Display headings.
		$LatestMax = ($LatestMax >= 11?$LatestMax-11:0);
		print '<table style="border-spacing:10px 0; font-family: monospace; vertical-align: bottom"><tr>'
			. '<td style="text-decoration: underline" align=right>&nbsp;&nbsp;PID</td>'
			. '<td style="text-decoration: underline">Last Update</td>'
			. '<td style="text-decoration: underline">FS % Used</td>'
			. '<td style="text-decoration: underline">Vault' . '&nbsp;'x($VaultMax-5) . '</td>'
			. '<td style="text-decoration: underline">File Size</td>'
			. '<td style="text-decoration: underline">Latest item' . '&nbsp;'x$LatestMax . '</td>'
			. '</tr>'
			. "\n";
		foreach my $logfile (sort(keys(%$LogRef))) {
			print	'<tr>'
				. '<td align=right>      '	. $LogRef->{$logfile}->{pid}		. '</td>'
				. '<td align=center>'		. $LogRef->{$logfile}->{mtime}		. '</td>'
				. '<td align=right>'		. $LogRef->{$logfile}->{percentfull}	. '&nbsp;&nbsp;&nbsp;</td>'
				. '<td>'			. $LogRef->{$logfile}->{vault}		. '</td>'
				. '<td align=right>'		. $LogRef->{$logfile}->{size}		. '&nbsp;&nbsp;</td>'
				. '<td>'			. $LogRef->{$logfile}->{lastfile}	. '</td>'
				. '</tr>'
				. "\n"
			;
		}
		print "</table>\n";
	}
	else {
		# Display as ordinary text.
		foreach my $logfile (sort(keys(%$LogRef))) {
			printf "%-8s %5d % 4s % 6s %-*s: %s\n",
				$LogRef->{$logfile}->{mtime},
				$LogRef->{$logfile}->{pid},
				$LogRef->{$logfile}->{percentfull},
				$LogRef->{$logfile}->{size},
				$VaultMax,
				$LogRef->{$logfile}->{vault},
				$LogRef->{$logfile}->{lastfile},
			;
		}
		
	}
}


# ---------------------------------------------------------
#
# opt_h: Usage
#
sub opt_h {

	use FindBin qw($RealBin $RealScript);

	system(qq<pod2text $RealBin/$RealScript | sed "s/%Prog/$Prog/g" | more>);
exit 1;
}

=pod

=head1 %Prog -  

=head3 Usage:  
        %Prog [flag1 ...]

        %Prog -h

=head3 Options:
        --option-set|-O config: Insert the "config" configuration options
                                from /usr/local/etc/%Prog.cfg
                                into the command line at this point.
	--count=n		Count: Run n update cycles and stop.  Default
				is to run forever.
	--interval=n		Interval: Update the status every n seconds.
				Default is 30 seconds.
        --verbose|-v:           Verbose: echo commands before running them.
				May be used multiple times to increase verbosity.
        --help|-h:              Help: display this panel

=head3 Parameters:
        (none)

=head3 Configuration files

Configuration data may be loaded from the configuration files.  These files
form key-value pairs that $Prog may reference.  The syntax of the file is:

        name: value

"name" must begin in column 1, and is case-insensitive.  Lines beginning
with white-space are treated as continuations of the previous line.  Blank
lines or lines beginning with # are ignored.

Several "names" are reserved, as follows:

=over

=item *
Alljobs: any value listed here is prepended to the command line before
command line processing begins.  This permits default options to be
set.

=item *
Defaultjob: any value listed here is used as the command line if no
command line options are specified.

=item *
Include: any value here is treated as another configuration file, which
is loaded immediately.

=back

=head3 Return codes:
        0       :       Normal termination
        1       :       Help panel displayed.
        2       :       Invalid or unrecognized command line options.
        3       :       Invalid or unrecognized command line option value.
        4       :       Incorrect command line parameters.
        5       :       Unexpected message found in output.


=head3 Notes:

=cut
